package AnalizadoresLexicoSintactico;
import java_cup.runtime.*;
import Instrucciones.ArbolSintactico;
import java.util.LinkedList;
import dataforge.ErrorSintactico;

parser code 
{:
    public LinkedList<ErrorSintactico> syntaxErrors = new LinkedList<>();

    public void syntax_error(Symbol s){
        if (s.value != null){
            syntaxErrors.add(new ErrorSintactico("Sintactico", s.value.toString(), s.left, s.right));
        }
    }
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
        System.out.println("Error Sintactico No Recuperado: "+s.value+" , linea: "+s.left+" columna: "+s.right);
    }
:}

terminal VAR, TIPO_VARIABLE, END;

terminal MENOS, DOS_PUNTOS, MENOR_QUE, PUNTO_COMA, IGUAL, MAYOR_QUE, CORCHETE_IZQUIERDO, CORCHETE_DERECHO;
terminal PARENTESIS_IZQUIERDO, PARENTESIS_DERECHO, COMA;

terminal EXEC, PRINT, COLUMN, CONSOLE, TIPO_GRAFICA;
terminal CADENA, ENTERO, DECIMAL , EXPRESION, EXPRESION_ARRAY, ARRAY, PROGRAM, END_PROGRAM, ARITMETICA, ESTADISTICA;
terminal TITULO_EJE, TITULO_GRAFICA;

//------------------------------------------------------------------------------
non terminal ArbolSintactico inicio, declaraciones, declaracion;

non terminal ArbolSintactico valor_grafica, valor_variable, contenido_array, contenido_grafica, lista_array;

//------------------------------------------------------------------------------
start with inicio;

inicio ::= PROGRAM:program declaraciones:sentencias END_PROGRAM:end_program

{:
    ArbolSintactico inicio = new ArbolSintactico("INICIO");
    inicio.AgregarHijo(new ArbolSintactico(program.toString()));
    inicio.AgregarHijo(sentencias);
    inicio.AgregarHijo(new ArbolSintactico(end_program.toString()));
    RESULT = inicio;   
:};

declaraciones ::= declaraciones:sentencias declaracion:sentencia 
{:
    ArbolSintactico declaraciones = new ArbolSintactico("DECLARACION");
    declaraciones.AgregarHijo(sentencias);
    declaraciones.AgregarHijo(sentencia);
    RESULT = declaraciones;
    
:}
                | declaracion:sentencia 

{:
    ArbolSintactico declaraciones = new ArbolSintactico("DECLARACION");
    declaraciones.AgregarHijo(sentencia);
    RESULT = declaraciones;
    
:};

//------------------------------------------------------------------------------
valor_variable ::=    ENTERO:valor1
{:
    ArbolSintactico valor = new ArbolSintactico("ASIGNACION VALOR VARIABLE");
    valor.AgregarHijo(new ArbolSintactico(valor1.toString()));
    RESULT = valor;
:}
                    | DECIMAL:valor1  
{:
    ArbolSintactico valor = new ArbolSintactico("ASIGNACION VALOR VARIABLE");
    valor.AgregarHijo(new ArbolSintactico(valor1.toString()));
    RESULT = valor;
:}
                    | CADENA:valor1 
{:
    ArbolSintactico valor = new ArbolSintactico("ASIGNACION VALOR VARIABLE");
    valor.AgregarHijo(new ArbolSintactico(valor1.toString()));
    RESULT = valor;
:}
                    | EXPRESION:valor1
{:
    ArbolSintactico valor = new ArbolSintactico("ASIGNACION VALOR VARIABLE");
    valor.AgregarHijo(new ArbolSintactico(valor1.toString()));
    RESULT = valor;
:}

                   | EXPRESION_ARRAY:valor1
{:
    ArbolSintactico valor = new ArbolSintactico("ASIGNACION VALOR VARIABLE");
    valor.AgregarHijo(new ArbolSintactico(valor1.toString()));
    RESULT = valor;
:}

                    | ARITMETICA:aritmetica PARENTESIS_IZQUIERDO:parentesis_izquierdo valor_variable:valor1 COMA:coma valor_variable:valor2 PARENTESIS_DERECHO:parentesis_derecho
{:
    ArbolSintactico valor = new ArbolSintactico("ASIGNACION VALOR VARIABLE");
    valor.AgregarHijo(new ArbolSintactico(aritmetica.toString()));
    valor.AgregarHijo(new ArbolSintactico(parentesis_izquierdo.toString()));
    valor.AgregarHijo(valor1);
    valor.AgregarHijo(new ArbolSintactico(coma.toString()));
    valor.AgregarHijo(valor2);
    valor.AgregarHijo(new ArbolSintactico(parentesis_derecho.toString()));
    RESULT = valor;
:}

                    | ESTADISTICA:estadistica PARENTESIS_IZQUIERDO:parentesis_izquierdo contenido_array:contenido PARENTESIS_DERECHO:parentesis_derecho
{:
    ArbolSintactico valor = new ArbolSintactico("ASIGNACION VALOR VARIABLE");
    valor.AgregarHijo(new ArbolSintactico(estadistica.toString()));
    valor.AgregarHijo(new ArbolSintactico(parentesis_izquierdo.toString()));
    valor.AgregarHijo(contenido);
    valor.AgregarHijo(new ArbolSintactico(parentesis_derecho.toString()));
    RESULT = valor;
:}


                   | ESTADISTICA:estadistica PARENTESIS_IZQUIERDO:parentesis_izquierdo valor_variable:valor2 PARENTESIS_DERECHO:parentesis_derecho
{:
    ArbolSintactico valor = new ArbolSintactico("ASIGNACION VALOR VARIABLE");
    valor.AgregarHijo(new ArbolSintactico(estadistica.toString()));
    valor.AgregarHijo(valor2);
    RESULT = valor;
:}

;
//------------------------------------------------------------------------------
declaracion ::= VAR:var DOS_PUNTOS:dos_puntos1 TIPO_VARIABLE:tipo_variable DOS_PUNTOS:dos_puntos2 DOS_PUNTOS:dos_puntos3 EXPRESION:expresion MENOR_QUE:menor_que MENOS:menos valor_variable:valor END:end PUNTO_COMA:punto_coma
{:
    ArbolSintactico declaracion = new ArbolSintactico("ASIGNACION VARIABLE");
    declaracion.AgregarHijo(new ArbolSintactico(var.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(dos_puntos1.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(tipo_variable.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(dos_puntos2.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(dos_puntos3.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(expresion.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(menor_que.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(menos.toString()));
    declaracion.AgregarHijo(valor);
    declaracion.AgregarHijo(new ArbolSintactico(end.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(punto_coma.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(String.valueOf(tipo_variableleft)));
    declaracion.AgregarHijo(new ArbolSintactico(String.valueOf(tipo_variableright)));
    RESULT = declaracion; 
:}
    | error
{:
    ArbolSintactico declaracion = new ArbolSintactico("Error");
    RESULT = declaracion; 
:}
;

//------------------------------------------------------------------------------
declaracion ::= ARRAY:array DOS_PUNTOS:dos_puntos1 TIPO_VARIABLE:tipo_variable DOS_PUNTOS:dos_puntos2 DOS_PUNTOS:dos_puntos3 EXPRESION_ARRAY:expresion_array MENOR_QUE:menor_que MENOS:menos contenido_array:contenido END:end PUNTO_COMA:punto_coma
{:
    ArbolSintactico declaracion = new ArbolSintactico("ASIGNACION ARREGLO");
    declaracion.AgregarHijo(new ArbolSintactico(array.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(dos_puntos1.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(tipo_variable.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(dos_puntos2.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(dos_puntos3.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(expresion_array.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(menor_que.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(menos.toString()));
    declaracion.AgregarHijo(contenido);
    declaracion.AgregarHijo(new ArbolSintactico(end.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(punto_coma.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(String.valueOf(tipo_variableleft)));
    declaracion.AgregarHijo(new ArbolSintactico(String.valueOf(tipo_variableright)));
    RESULT = declaracion;
:}
;

contenido_array ::= CORCHETE_IZQUIERDO:corchete_izquierdo lista_array:lista CORCHETE_DERECHO:corchete_derecho
{:
    ArbolSintactico contenido_array = new ArbolSintactico("ASIGNACION CONTENIDO ARREGLO");
    contenido_array.AgregarHijo(new ArbolSintactico(corchete_izquierdo.toString()));
    contenido_array.AgregarHijo(lista);
    contenido_array.AgregarHijo(new ArbolSintactico(corchete_derecho.toString()));
    RESULT = contenido_array;
    
:};

lista_array ::= lista_array:lista COMA:coma valor_variable:valor_var 
{:
    ArbolSintactico lista_array = new ArbolSintactico("ASIGNACION LISTA ARREGLO");
    lista_array.AgregarHijo(lista);
    lista_array.AgregarHijo(new ArbolSintactico(coma.toString()));
    lista_array.AgregarHijo(valor_var);
    RESULT = lista_array;
:}
        | valor_variable:valor1
{:
    ArbolSintactico valor = new ArbolSintactico("ASIGNACION LISTA ARREGLO");
    valor.AgregarHijo(valor1);
    RESULT = valor;   
:}
;

//------------------------------------------------------------------------------
declaracion ::= CONSOLE:console DOS_PUNTOS:dos_puntos1 DOS_PUNTOS:dos_puntos2 PRINT:print IGUAL:igual lista_array:lista END:end PUNTO_COMA:punto_coma
{:
    ArbolSintactico declaracion = new ArbolSintactico("ASIGNACION IMPRIMIR");
    declaracion.AgregarHijo(new ArbolSintactico(console.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(dos_puntos1.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(dos_puntos2.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(print.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(igual.toString()));
    declaracion.AgregarHijo(lista);
    declaracion.AgregarHijo(new ArbolSintactico(end.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(punto_coma.toString()));
    RESULT = declaracion;
:}
;
//------------------------------------------------------------------------------
declaracion ::= CONSOLE:console DOS_PUNTOS:dos_puntos1 DOS_PUNTOS:dos_puntos2 COLUMN:column IGUAL:igual valor_variable:valor_var1 MENOS:menos MAYOR_QUE:mayor_que valor_variable:valor_var2 END:end PUNTO_COMA:punto_coma
{:
    ArbolSintactico declaracion = new ArbolSintactico("ASIGNACION IMPRIMIR");
    declaracion.AgregarHijo(new ArbolSintactico(console.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(dos_puntos1.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(dos_puntos2.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(column.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(igual.toString()));
    declaracion.AgregarHijo(valor_var1);
    declaracion.AgregarHijo(new ArbolSintactico(menos.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(mayor_que.toString()));
    declaracion.AgregarHijo(valor_var2);
    declaracion.AgregarHijo(new ArbolSintactico(end.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(punto_coma.toString()));
    RESULT = declaracion;
:};

declaracion ::= CONSOLE DOS_PUNTOS DOS_PUNTOS COLUMN IGUAL valor_variable:valor_var1 MENOS MAYOR_QUE contenido_array:contenido END:end PUNTO_COMA:punto_coma
{:
    ArbolSintactico declaracion = new ArbolSintactico("ASIGNACION IMPRIMIR");
    declaracion.AgregarHijo(valor_var1);
    declaracion.AgregarHijo(contenido);
    RESULT = declaracion;
:};

//------------------------------------------------------------------------------
valor_grafica ::=  TITULO_GRAFICA:titulo DOS_PUNTOS:dos_puntos1 DOS_PUNTOS:dos_puntos2 TIPO_VARIABLE:tipo_val IGUAL:igual valor_variable:valor_val END:end PUNTO_COMA:punto_coma
{:
    ArbolSintactico contenido = new ArbolSintactico("ASIGNACION CONTENIDO GRAFICA");
    contenido.AgregarHijo(new ArbolSintactico(titulo.toString()));
    contenido.AgregarHijo(new ArbolSintactico(dos_puntos1.toString()));
    contenido.AgregarHijo(new ArbolSintactico(dos_puntos2.toString()));
    contenido.AgregarHijo(new ArbolSintactico(tipo_val.toString()));
    contenido.AgregarHijo(new ArbolSintactico(igual.toString()));
    contenido.AgregarHijo(valor_val);
    contenido.AgregarHijo(new ArbolSintactico(end.toString()));
    contenido.AgregarHijo(new ArbolSintactico(punto_coma.toString()));
    RESULT = contenido;
:}
            | TITULO_EJE:titulo DOS_PUNTOS:dos_puntos1 DOS_PUNTOS:dos_puntos2 TIPO_VARIABLE:tipo_val IGUAL:igual contenido_array:valor_val END:end PUNTO_COMA:punto_coma
{:
    ArbolSintactico contenido = new ArbolSintactico("ASIGNACION CONTENIDO GRAFICA");
    contenido.AgregarHijo(new ArbolSintactico(titulo.toString()));
    contenido.AgregarHijo(new ArbolSintactico(dos_puntos1.toString()));
    contenido.AgregarHijo(new ArbolSintactico(dos_puntos2.toString()));
    contenido.AgregarHijo(new ArbolSintactico(tipo_val.toString()));
    contenido.AgregarHijo(new ArbolSintactico(igual.toString()));
    contenido.AgregarHijo(valor_val);
    contenido.AgregarHijo(new ArbolSintactico(end.toString()));
    contenido.AgregarHijo(new ArbolSintactico(punto_coma.toString()));
    RESULT = contenido;
:}
            | TITULO_EJE:titulo DOS_PUNTOS:dos_puntos1 DOS_PUNTOS:dos_puntos2 TIPO_VARIABLE:tipo_val IGUAL:igual valor_variable:valor_val END:end PUNTO_COMA:punto_coma
{:
    ArbolSintactico contenido = new ArbolSintactico("ASIGNACION CONTENIDO GRAFICA");
    contenido.AgregarHijo(new ArbolSintactico(titulo.toString()));
    contenido.AgregarHijo(new ArbolSintactico(dos_puntos1.toString()));
    contenido.AgregarHijo(new ArbolSintactico(dos_puntos2.toString()));
    contenido.AgregarHijo(new ArbolSintactico(tipo_val.toString()));
    contenido.AgregarHijo(new ArbolSintactico(igual.toString()));
    contenido.AgregarHijo(valor_val);
    contenido.AgregarHijo(new ArbolSintactico(end.toString()));
    contenido.AgregarHijo(new ArbolSintactico(punto_coma.toString()));
    RESULT = contenido;
:}
;
//------------------------------------------------------------------------------
declaracion::= TIPO_GRAFICA:tipo_grafica1 PARENTESIS_IZQUIERDO:par_iz contenido_grafica:cont_graph EXEC:exec TIPO_GRAFICA:tipo_grafica2 END:end1 PUNTO_COMA:punto_coma1 PARENTESIS_DERECHO:par_de END:end2 PUNTO_COMA:punto_coma2
{:
    ArbolSintactico declaracion = new ArbolSintactico("ASIGNACION GRAFICA");
    declaracion.AgregarHijo(new ArbolSintactico(tipo_grafica1.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(par_iz.toString()));
    declaracion.AgregarHijo(cont_graph);
    declaracion.AgregarHijo(new ArbolSintactico(exec.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(tipo_grafica2.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(end1.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(punto_coma1.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(par_de.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(end2.toString()));
    declaracion.AgregarHijo(new ArbolSintactico(punto_coma2.toString()));
    RESULT = declaracion;
:};

contenido_grafica::= contenido_grafica:lista valor_grafica:valor_g 
{:
    ArbolSintactico contenido_grafica= new ArbolSintactico("ASIGNACION LISTA GRAFICA");
    contenido_grafica.AgregarHijo(lista);
    contenido_grafica.AgregarHijo(valor_g);
    RESULT = contenido_grafica;
:}
                |valor_grafica:valor_g
{:
    ArbolSintactico contenido_grafica= new ArbolSintactico("ASIGNACION LISTA GRAFICA");
    contenido_grafica.AgregarHijo(valor_g);
    RESULT = contenido_grafica;
:}
;


